apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        group "com.box"

        // Set version code and name
        def env = System.getenv()
        def git_count = env['GIT_COUNT'] as Integer
        if (git_count != null) {
            versionCode git_count + 22000
            versionName "2.99." + git_count + "-SNAPSHOT"
            version "2.99." + git_count + "-SNAPSHOT"
        } else {
            versionCode 22000
            versionName "2.2.0"
            version "2.2.0"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        jumboMode = true
        javaMaxHeapSize = "4g"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.1'
    // If using local dependency instead of maven, use the path pointing to the content sdk to replace
    // the maven dependency. e.g.:compile project(':box-android-content-sdk:box-content-sdk')
    compile 'com.box:box-android-sdk:4.0.10'
    compile "com.splitwise:tokenautocomplete:2.0.5@aar"
}

apply from: '../maven_push.gradle'
